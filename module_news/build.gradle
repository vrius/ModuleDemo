if (IS_MODULE.toBoolean()){
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        if (IS_MODULE.toBoolean()){
            //组件开发模式下需要设置applicationId
            applicationId "com.discovery.module.news"
        }
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //注解处理器相关，所有业务组件都需要添加
        javaCompileOptions {
            annotationProcessorOptions {
                //此处的AROUTER_MODULE_NAME是定义好的，不要随笔写，否则报错
                arguments = [ AROUTER_MODULE_NAME : project.getName() ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //在组件开发模式下需要完整的AndroidManifest.xml文件，确保业务组件能单独运行
    sourceSets {
        main {
            if (IS_MODULE.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //非组件开发模式下，忽略debug下所有java文件的打包
                java{
                    exclude 'debug/**'
                }
            }

        }
    }

    //规范组件资源文件ID命名必须以main_开头，这是为了防止各个组件中资源命名冲突
    resourcePrefix "news_"

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':module_common')
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
}
